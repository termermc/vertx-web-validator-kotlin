plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
    id 'org.jetbrains.dokka' version '1.4.30'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

archivesBaseName = 'vertx-web-validator-kotlin'
group = 'net.termer.vertx.kotlin.validation'
version = '1.0.1'

repositories {
    jcenter()
}

dependencies {
    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // Vert.x
    implementation 'io.vertx:vertx-core:4.0.0'
    implementation 'io.vertx:vertx-web:4.0.0'

    // Use the Kotlin test library.
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    // Testing
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    // Docs
    dokkaHtmlPlugin("org.jetbrains.dokka:kotlin-as-java-plugin:1.4.30")
}

test {
    useJUnitPlatform()
}

tasks {
    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    javadoc {
        dependsOn('dokkaJavadoc')
    }
}

tasks.create('javadocJar', org.gradle.jvm.tasks.Jar) {
    dependsOn(dokkaJavadoc)
    archiveClassifier.set("javadoc")
    from("build/dokka/javadoc")
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

dokkaHtml {
    moduleName.set('vertx-web-validator-kotlin')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.termer.vertx.kotlin.validation'
            artifactId = 'vertx-web-validator-kotlin'
            version = '1.0.1'

            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'vertx-web-validator-kotlin'
                description = 'A simple Kotlin library for null-safe Vert.x Web request validation'
                url = 'https://github.com/termermc/vertx-web-validator-kotlin'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'termer'
                        name = 'Michael Termer'
                        email = 'termer@protonmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/termermc/vertx-web-validator-kotlin.git'
                    developerConnection = 'scm:git:ssh://github.com/termermc/vertx-web-validator-kotlin.git'
                    url = 'https://github.com/termermc/vertx-web-validator-kotlin'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}